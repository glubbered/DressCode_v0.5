//this example shows you how to write the function you used from 
//the first level using a loop!

x = 200;
y = 200;
width =200;
height=40;
num = 6;
radius=70;

//create an empty group
g = group();

//this is the loop- 
//it repeats the number of times specificed in the "num" variable
repeat i=0:num:
	
	//calculate the angle based on the number of shapes
	theta = 360/num*i;
	
	//create your shape (try a rectangle or polygon here, 
	//or any union of two shapes
	shape1 = union(ellipse(x,y,width,height),ellipse(x,y,height,width));
	
	//move your shape using polar coordinates
	headingBy(shape1,theta,radius);
        //rotate the shape to the value of theta
	rotate(shape1,theta);

	//add shape to group
	add(g,shape1);
end

//sets the shapes to an outline
g = expand(g);
//merges the group together into one shape
g = merge(g);



