
/*-----------------Your code from the last level goes here--------------------*/
x = 200;
y = 200;
width =50;
height=10;
num = 6;
radius=20;

//create an empty group
g = group();

//this is the loop- 
//it repeats the number of times specified in the "num" variable
repeat i=0:num:
	
	//calculate the angle based on the number of shapes
	theta = 360/num*i;
	
	//create your shape (try a rectangle or polygon here, 
	//or any union of two shapes
	shape1 = union(ellipse(x,y,width,height),ellipse(x,y,height,width));


	
	headingBy(shape1,theta,radius);
	rotate(shape1,theta);
	
	//add shape to group
	add(g,shape1);
end

//sets the shapes to an outline
g = expand(g);
//merges the group together into one shape
g = merge(g);

/*-----------------Your code from the last level goes here --------------------*/

//you can pattern your designs from the last level 
//in different ways using the grid, arc and wave methods

//grid(drawable, colNum, rowNum, gapX, gapY);
//drawable: the target shape to be copied
//colNum: the number of columns in the grid
//rowNum: the number of rows in the grid 
//gapX: the amount of spacing between the columns
//gapY: the amount of spacing between the rows 

//arc(drawable, num, width,height,degrees);
//drawable: the target shape to be copied
//num: the number of shapes in the arc
//width: the width of the arc
//height: the height of the arc
//degrees: the number of degrees of the arc (180 is a half circle, 360 is a full circle)
 

//wave(drawable, num, amp,freq, width);
//drawable: the target shape to be copied
//num: the number of shapes in the wave
//amp: the height(amplitude) of the wave
//frequency: the number of cycles(crests) in the wave
//width: the width of the entire wave

//example wave method using the design from above
w = wave(g,12,50,2,WIDTH);

//clips the wave within the bracelet template
clipToBracelet(w);


